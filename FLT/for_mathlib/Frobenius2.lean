/-
Copyright (c) 2024 Jou Glasheen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jou Glasheen, Amelia Livingston, Jujian Zhang, Kevin Buzzard
-/
import Mathlib.RingTheory.DedekindDomain.Ideal
import Mathlib.RingTheory.IntegralRestrict
import Mathlib.RingTheory.Ideal.QuotientOperations
import Mathlib.FieldTheory.Cardinality

/-

# Frobenius elements

Frobenius elements in Galois groups.

## Statement of the theorem

Say `L/K` is a finite Galois extension of number fields, with integer rings `B/A`,
and say `Q` is a maximal ideal of `B` dividing `P` of `A`. This file contains the
construction of an element `Frob Q` in `Gal(L/K)`, and a proof that
modulo `Q` it raises elements to the power `q := |A/P|`.

More generally, our theory works in the "ABKL" setting, with `B/A` a finite extension of
Dedekind domains, and the corresponding extension `L/K` of fields of fractions is
assumed finite and Galois. Given `Q/P` a compatible pair of maximal ideals, under the
assumption that `A/P` is a finite field of size `q`, we construct an element `Frob_Q`
in `Gal(L/K)` and prove:

1) `Frob_Q ‚Ä¢ Q = Q`;
2) `x ^ q ‚â° Frob_Q x (mod Q)`.

Examples where these hypotheses hold are:

1) Finite Galois extensions of number fields and their integers or `S`-integers;
2) Finite Galois extensions of function fields over finite fields, and their `S`-integers for
   `S` a nonempty set of places;
3) Finite Galois extensions of finite fields L/K where B/A=L/K and Q/P=0/0 (recovering the
classical theory of Frobenius elements)

Note that if `Q` is ramified, there is more than one choice of `Frob_Q` which works;
for example if `L=‚Ñö(i)` and `K=‚Ñö` then both the identity and complex conjugation
work for `Frob Q` if `Q=(1+i)`, and `Frob` returns a random one (i.e. it's opaque; all we know
is that it satisfies the two properties).

## The construction

We follow a proof in a footnote of a book by Milne. **TODO** which book

The Galois orbit of `Q` consists of `Q` and possibly some other primes `Q'`. The unit group
`(B/Q)À£` is finite and hence cyclic; so by the Chinese Remainder Theorem we may choose y ‚àà B
which reduces to a generator mod Q and to 0 modulo all other Q' in the Galois orbit of Q.

The polynomial `F = ‚àè (X - œÉ y)`, the product running over `œÉ` in the Galois group, is in `B[X]`
and is Galois-stable so is in fact in `A[X]`. Hence if `Fbar` is `F mod Q`
then `Fbar` has coefficients in `A/P=ùîΩ_q` and thus `Fbar(y^q)=Fbar(y)^q=0`, meaning that `y^q`
is a root of `F` mod `Q` and thus congruent to `œÉ y mod Q` for some `œÉ`. We define `Frob Q` to
be this `œÉ`.

## The proof

We know `œÉ y ‚â° y ^ q ‚â† 0 mod Q`. Hence `œÉ y ‚àâ Q`, and thus `y ‚àâ œÉ‚Åª¬π Q`. But `y ‚àà Q'` for all nontrivial
conjugates `Q'` of `Q`, hence `œÉ‚Åª¬π Q = Q` and thus `Q` is `œÉ`-stable.

Hence `œÉ` induces a field automorphism of `B/Q` and we know it's `x ‚Ü¶ x^q` on a generator,
so it's `x ‚Ü¶ x^q` on everything.

## Note

This was Jou Glasheen's final project for Kevin Buzzard's Formalising Mathematics course.

-/
variable (A : Type*) [CommRing A] {B : Type*} [CommRing B] [Algebra A B]

-- was Eric going to put this somewhere?
instance : MulDistribMulAction (B ‚âÉ‚Çê[A] B) (Ideal B) where
  smul œÉ I := Ideal.comap œÉ.symm I
  one_smul I := I.comap_id
  smul_one œÉ := by simp only [Ideal.one_eq_top]; rfl
  mul_smul _ _ _ := rfl
  smul_mul œÉ I J := by
    refine le_antisymm (fun x ‚Ü¶ ?_ : Ideal.comap _ _ ‚â§ _) (Ideal.le_comap_mul _)
    obtain ‚ü®y, rfl‚ü© : ‚àÉ y, œÉ y = x := ‚ü®œÉ.symm x, œÉ.apply_symm_apply x‚ü©
    rw [Ideal.mem_comap, AlgEquiv.symm_apply_apply, ‚Üê Ideal.mem_comap]
    revert y
    refine Ideal.mul_le.mpr (fun r hr s hs ‚Ü¶ ?_)
    simp only [Ideal.mem_comap, map_mul]
    exact Ideal.mul_mem_mul (Ideal.mem_comap.2 (by simp [hr])) (Ideal.mem_comap.2 <| by simp [hs])

/-
-- Auxiliary lemma: if `Q` is a maximal ideal of a non-field Dedekind Domain `B` with a Galois action
-- and if `b ‚àà B` then there's an element of `B` which is `b` mod `Q` and `0` modulo all the other
-- Galois conjugates of `Q`.
-- -/
-- lemma DedekindDomain.exists_y [IsDedekindDomain B] [Fintype (B ‚âÉ‚Çê[A] B)]
--     [DecidableEq (Ideal B)] (Q : Ideal B) [Q.IsMaximal] (b : B) : ‚àÉ y : B,
--     y - b ‚àà Q ‚àß ‚àÄ Q' : Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí y ‚àà Q' := by
--   let O : Set (Ideal B) := MulAction.orbit (B ‚âÉ‚Çê[A] B) Q
--   have hO : O.Finite := Set.finite_range _
--   have hPrime : ‚àÄ Q' ‚àà hO.toFinset, Prime Q' := by
--     intro Q' hQ'
--     rw [Set.Finite.mem_toFinset] at hQ'
--     obtain ‚ü®œÉ, rfl‚ü© := hQ'
--     apply (MulEquiv.prime_iff <| MulDistribMulAction.toMulEquiv (Ideal B) œÉ).mp
--     refine Q.prime_of_isPrime (Q.bot_lt_of_maximal hB).ne' ?_
--     apply Ideal.IsMaximal.isPrime inferInstance
--   obtain ‚ü®y, hy‚ü© := IsDedekindDomain.exists_forall_sub_mem_ideal (s := hO.toFinset) id (fun _ ‚Ü¶ 1)
--     hPrime (fun _ _ _ _ ‚Ü¶ id) (fun Q' ‚Ü¶ if Q' = Q then b else 0)
--   simp only [Set.Finite.mem_toFinset, id_eq, pow_one] at hy
--   refine ‚ü®y, ?_, ?_‚ü©
--   ¬∑ specialize hy Q ‚ü®1, by simp‚ü©
--     simpa only using hy
--   ¬∑ rintro Q' ‚ü®œÉ, rfl‚ü© hQ'
--     specialize hy (œÉ ‚Ä¢ Q) ‚ü®œÉ, by simp‚ü©
--     simp_all

lemma exists_y [Fintype (B ‚âÉ‚Çê[A] B)] [DecidableEq (Ideal B)] (Q : Ideal B) [Q.IsMaximal] (b : B) :
    ‚àÉ y : B, y - b ‚àà Q ‚àß ‚àÄ Q' : Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí y ‚àà Q' := by
  let O : Set (Ideal B) := MulAction.orbit (B ‚âÉ‚Çê[A] B) Q
  have hO' : Finite (O : Type _) := Set.finite_range _
  have hmax (I : O) : Ideal.IsMaximal (I : Ideal B) := by
    rcases I with ‚ü®_, œÉ, rfl‚ü©
    exact Ideal.comap_isMaximal_of_surjective _ (AlgEquiv.surjective œÉ.symm)
  have hPairwise : Pairwise fun (I : O) (J : O) ‚Ü¶ IsCoprime (I : Ideal B) J := fun x y h ‚Ü¶ ‚ü®1, 1, by
    simp only [Ideal.one_eq_top, Ideal.top_mul]
    exact Ideal.IsMaximal.coprime_of_ne (hmax x) (hmax y) <| mt Subtype.ext h‚ü©
  obtain ‚ü®y, hy‚ü© := Ideal.exists_forall_sub_mem_ideal (Œπ := O) hPairwise
    (fun J ‚Ü¶ if J = ‚ü®Q, 1, rfl‚ü© then b else 0)
  refine ‚ü®y, ?_, ?_‚ü©
  ¬∑ specialize hy ‚ü®Q, 1, Q.comap_id‚ü©
    simpa only using hy
  ¬∑ rintro Q' ‚ü®œÉ, rfl‚ü© hQ'
    specialize hy ‚ü®œÉ ‚Ä¢ Q, œÉ, rfl‚ü©
    simp_all

variable (Q : Ideal B) [Q.IsMaximal] [Fintype (B ‚ß∏ Q)]

noncomputable abbrev g : (B ‚ß∏ Q)À£ := (IsCyclic.exists_monoid_generator (Œ± := (B ‚ß∏ Q)À£)).choose

lemma g_spec : ‚àÄ (z : (B ‚ß∏ Q)À£), z ‚àà Submonoid.powers (g Q) :=
  (IsCyclic.exists_monoid_generator (Œ± := (B ‚ß∏ Q)À£)).choose_spec

noncomputable abbrev b : B := (Ideal.Quotient.mk_surjective (g Q : B ‚ß∏ Q)).choose

lemma b_spec : ((b Q : B) : B ‚ß∏ Q) = g Q := (Ideal.Quotient.mk_surjective (g Q : B ‚ß∏ Q)).choose_spec

variable [Fintype (B ‚âÉ‚Çê[A] B)] [DecidableEq (Ideal B)]

noncomputable abbrev y : B :=
  (exists_y A Q (b Q)).choose

lemma y_spec : (y A Q) - (b Q) ‚àà Q ‚àß
    ‚àÄ Q' : Ideal B, Q' ‚àà MulAction.orbit (B ‚âÉ‚Çê[A] B) Q ‚Üí Q' ‚â† Q ‚Üí (y A Q) ‚àà Q' :=
  (exists_y A Q (b Q)).choose_spec

open Polynomial BigOperators

/-- `F : B[X]` defined to be a product of linear factors `(X - œÑ ‚Ä¢ Œ±)`; where
`œÑ` runs over `L ‚âÉ‚Çê[K] L`, and `Œ± : B` is an element which generates `(B ‚ß∏ Q)À£`
and lies in `œÑ ‚Ä¢ Q` for all `œÑ ‚àâ (decomposition_subgroup_Ideal'  A K L B Q)`.-/
noncomputable abbrev F : B[X] := ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q)))

lemma F_spec : F A Q = ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q))) := rfl

variable {A Q} in
open Finset in
lemma F.smul_eq_self (œÉ :  B ‚âÉ‚Çê[A] B)  : œÉ ‚Ä¢ (F A Q) = F A Q := calc
  œÉ ‚Ä¢ F A Q = œÉ ‚Ä¢ ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C (œÑ ‚Ä¢ (y A Q))) := by rw [F_spec]
  _         = ‚àè œÑ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ (X - C (œÑ ‚Ä¢ (y A Q))) := smul_prod
  _         = ‚àè œÑ : B ‚âÉ‚Çê[A] B, (X - C ((œÉ * œÑ) ‚Ä¢ (y A Q))) := by simp [smul_sub]
  _         = ‚àè œÑ' : B ‚âÉ‚Çê[A] B, (X - C (œÑ' ‚Ä¢ (y A Q))) := Fintype.prod_bijective (fun œÑ ‚Ü¶ œÉ * œÑ)
                                                      (Group.mulLeft_bijective œÉ) _ _ (fun _ ‚Ü¶ rfl)
  _         = F A Q := by rw [F_spec]

open scoped algebraMap

noncomputable local instance : Algebra A[X] B[X] :=
  RingHom.toAlgebra (Polynomial.mapRingHom (Algebra.toRingHom))

@[simp, norm_cast]
lemma coe_monomial (n : ‚Ñï) (a : A) : ((monomial n a : A[X]) : B[X]) = monomial n (a : B) := by
  change ((Polynomial.mapRingHom (Algebra.toRingHom : A ‚Üí+* B))) (monomial n a : A[X]) = monomial n (a : B)
  simp
  rfl

lemma F.descent (h : ‚àÄ b : B, (‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = a) :
    ‚àÉ m : A[X], (m : B[X]) = F A Q := by
  choose f hf using h
  classical
  let f' : B ‚Üí A := fun b ‚Ü¶ if h : ‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ b = b then f b h else 37
  let m := (F A Q).sum (fun n r ‚Ü¶ Polynomial.monomial n (f' r))
  use m
  ext N
  simp only [m, sum]
  push_cast
  simp_rw [finset_sum_coeff, ‚Üê lcoeff_apply, lcoeff_apply, coeff_monomial]
  simp only [Finset.sum_ite_eq', mem_support_iff, ne_eq, ite_not, f']
  symm
  split
  ¬∑ next h1 => exact h1
  ¬∑ next h1 =>
    rw [dif_pos <| fun œÉ ‚Ü¶ ?_]
    ¬∑ refine hf ?_ ?_
    ¬∑ nth_rw 2 [‚Üê F.smul_eq_self œÉ]
      rfl

variable (isGalois : ‚àÄ b : B, (‚àÄ œÉ : B ‚âÉ‚Çê[A] B, œÉ ‚Ä¢ b = b) ‚Üí ‚àÉ a : A, b = a)

noncomputable abbrev m := (F.descent A Q isGalois).choose

lemma m_spec : ((m A Q isGalois) : B[X]) = F A Q := (F.descent A Q isGalois).choose_spec

lemma m_spec' : (m A Q isGalois).map (algebraMap A B) = F A Q := by
  rw [‚Üê m_spec A Q isGalois]
  rfl

lemma F.y_eq_zero : (F A Q).eval (y A Q) = 0 := by
  simp [F_spec, eval_prod, Finset.prod_eq_zero (Finset.mem_univ (1 : B ‚âÉ‚Çê[A] B))]

example : B ‚Üí+* B ‚ß∏ Q := algebraMap _ _

-- lemma F.mod_Q_y_eq_zero : ((F A Q).map (algebraMap B (B‚ß∏Q))).eval (algebraMap B (B‚ß∏Q) (y A Q)) = 0 := by
--   rw [Polynomial.eval_map]
--   simp
--   simp only [Polynomial.eval_map, Ideal.Quotient.algebraMap_eq, eval‚ÇÇ_at_apply, map_zero, F.y_eq_zero A Q]

variable (P : Ideal A) [P.IsMaximal] [Algebra (A ‚ß∏ P) (B ‚ß∏ Q)] [IsScalarTower A (A‚ß∏P) (B‚ß∏Q)]

lemma m.mod_P_y_eq_zero : (m A Q isGalois).eval‚ÇÇ (algebraMap A (B‚ß∏Q)) (algebraMap B (B‚ß∏Q) (y A Q)) = 0 := by
  rw [show algebraMap A (B‚ß∏Q) = (algebraMap B (B‚ß∏Q)).comp (algebraMap A B) from IsScalarTower.algebraMap_eq A B (B ‚ß∏ Q)]
  rw [‚Üêeval‚ÇÇ_map]
  change eval‚ÇÇ _ _ (m A Q isGalois : B[X]) = _
  simp [m_spec A Q isGalois, eval_map, F.y_eq_zero]

noncomputable abbrev mmodP := (m A Q isGalois).map (algebraMap A (A‚ß∏P))

open scoped Polynomial

-- example (K : Type*) [Field K] [Fintype K] : ‚àÉ p n : ‚Ñï, p.Prime ‚àß Fintype.card K = p ^ n ‚àß CharP K p := by
--   obtain ‚ü®p, n, h‚ÇÅ, h‚ÇÇ‚ü© := FiniteField.card' K
--   refine ‚ü®p, n.val, h‚ÇÅ, h‚ÇÇ, ?_‚ü©
--   have : (p ^ n.val : K) = 0 := mod_cast h‚ÇÇ ‚ñ∏ Nat.cast_card_eq_zero K
--   rw [CharP.charP_iff_prime_eq_zero h‚ÇÅ]
--   simpa only [ne_eq, PNat.ne_zero, not_false_eq_true, pow_eq_zero_iff] using this

-- mathlib
lemma bar (k : Type*) [Field k] [Fintype k] : ‚àÉ n : ‚Ñï, ringExpChar k ^ n = Fintype.card k := by
  sorry

-- mathlib
lemma foo (k : Type*) [Field k] [Fintype k] (f : k[X]) (L : Type*) [CommRing L] [Algebra k L]
    (t : L) : f.eval‚ÇÇ (algebraMap k L) (t^(Fintype.card k)) =
              (f.eval‚ÇÇ (algebraMap k L) t)^(Fintype.card k) := by
  obtain ‚ü®n, hn‚ü© := bar k
  induction f using Polynomial.induction_on
  ¬∑ simp [‚Üê map_pow, FiniteField.pow_card]
  ¬∑ next g h h1 h2 =>
    simp only [Polynomial.eval‚ÇÇ_add, h1, h2]
    rw [‚Üê hn]
    by_cases hL : Nontrivial L
    ¬∑ haveI := expChar_of_injective_algebraMap (NoZeroSMulDivisors.algebraMap_injective k L) (ringExpChar k)
      rw [add_pow_expChar_pow]
    ¬∑ apply (not_nontrivial_iff_subsingleton.mp hL).elim
  ¬∑ simp only [Polynomial.eval‚ÇÇ_mul, Polynomial.eval‚ÇÇ_C, Polynomial.eval‚ÇÇ_X_pow, mul_pow,
                 ‚Üê map_pow, pow_right_comm, FiniteField.pow_card]

variable [Fintype (A‚ß∏P)]
-- (m-bar)(y^q)=0 in B/Q
lemma m.mod_P_y_pow_q_eq_zero :
    (m A Q isGalois).eval‚ÇÇ (algebraMap A (B‚ß∏Q)) ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P)))
    = 0 := by
  suffices ((m A Q isGalois).map (algebraMap A (A‚ß∏P))).eval‚ÇÇ (algebraMap (A‚ß∏P) (B‚ß∏Q))
    ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P))) = 0 by
    rwa [eval‚ÇÇ_map, ‚Üê IsScalarTower.algebraMap_eq A (A ‚ß∏ P) (B ‚ß∏ Q)] at this
  let foobar : Field (A‚ß∏P) := ((Ideal.Quotient.maximal_ideal_iff_isField_quotient P).mp ‚Äπ_‚Ä∫).toField
  rw [foo, eval‚ÇÇ_map, ‚Üê IsScalarTower.algebraMap_eq A (A ‚ß∏ P) (B ‚ß∏ Q), m.mod_P_y_eq_zero, zero_pow]
  exact Fintype.card_ne_zero

lemma F.mod_Q_y_pow_q_eq_zero : (F A Q).eval‚ÇÇ (algebraMap B (B‚ß∏Q)) ((algebraMap B (B‚ß∏Q) (y A Q)) ^ (Fintype.card (A‚ß∏P))) = 0 := by
  rw [‚Üê m_spec' A Q isGalois, eval‚ÇÇ_map]--, m.mod_P_y_pow_q_eq_zero]
  rw [‚Üê IsScalarTower.algebraMap_eq A B (B ‚ß∏ Q), m.mod_P_y_pow_q_eq_zero]

lemma exists_thing : ‚àÉ œÉ : B ‚âÉ‚Çê[A] B, œÉ (y A Q) - (y A Q) ^ (Fintype.card (A‚ß∏P)) ‚àà Q := by
  have := F.mod_Q_y_pow_q_eq_zero A Q isGalois P
  rw [F_spec] at this
  rw [eval‚ÇÇ_finset_prod] at this
  rw [Finset.prod_eq_zero_iff] at this
  obtain ‚ü®œÉ, -, hœÉ‚ü© := this
  use œÉ
  simp only [Ideal.Quotient.algebraMap_eq, AlgEquiv.smul_def, eval‚ÇÇ_sub, eval‚ÇÇ_X, eval‚ÇÇ_C,
    sub_eq_zero] at hœÉ
  exact (Submodule.Quotient.eq Q).mp (hœÉ.symm)

noncomputable abbrev Frob := (exists_thing A Q isGalois P).choose

lemma Frob_spec : (Frob A Q isGalois P) (y A Q) - (y A Q) ^ (Fintype.card (A‚ß∏P)) ‚àà Q :=
  (exists_thing A Q isGalois P).choose_spec

/- maths proof:

1) œÉ Q = Q. Because if not then Q ‚â† œÉ‚Åª¬π Q so y ‚àà œÉ‚Åª¬π Q so œÉ y ‚àà Q so y^p ‚àà Q so y ‚àà Q so #
2) œÉ is x ^ #A/P mod Q
3) Application to number fields
-/
